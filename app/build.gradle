plugins {
    id 'com.android.application'
    id 'org.greenrobot.greendao' // apply plugin
}

android {
    namespace 'com.actor.cnpc_qhse_exams'
    compileSdk 33

    buildFeatures {
        //使用viewBinding
        viewBinding = true
    }

    defaultConfig {
        applicationId "com.actor.cnpc_qhse_exams"
        minSdk 24
        targetSdk 33
        versionCode 20250421
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias 'actor'
            keyPassword '123456'
            storeFile file('actorKey.jks')
            storePassword '123456'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "TiKu_v${variant.versionName}_${variant.flavorName}_${buildTime()}_${variant.buildType.name}.apk"
        }
    }

    greendao {
        schemaVersion 1                 //指定数据库schema版本号，迁移等操作会用到
        /**
         * dao的包名，包名默认是entity所在的包。
         * (请务必使用此包名, 否则这个Utils会找不到DaoMaster和DaoSession)。
         * (如果你实在不用这个包名, 那么可以将本Utils拷贝到自己项目, 使用拷贝的Utils也可以!)。
         */
         daoPackage 'com.greendao.gen'
         targetGenDir 'src/main/java'    //生成数据库文件的目录(请确保是这个目录, 否则本工具类将无法使用)
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    /**
     * Github开源框架
     */
    //https://github.com/actor20170211030627/MyAndroidFrameWork
    implementation ('com.github.actor20170211030627.MyAndroidFrameWork:library:2.1.9') {
        exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        exclude group: 'com.google.code.gson', module: 'gson'
        exclude group: 'com.github.bumptech.glide', module: 'glide'
        exclude group: 'com.github.getActivity', module: 'EasyHttp'
//        exclude group: 'com.tencent', module: 'mmkv-static'
        exclude group: 'com.github.li-xiaojun', module: 'XPopup'
    }
    //图片选择
//    implementation 'com.github.actor20170211030627.MyAndroidFrameWork:picture_selector:2.1.9'
    implementation 'com.github.actor20170211030627.MyAndroidFrameWork:database:2.1.9'
//    implementation 'com.github.actor20170211030627.MyAndroidFrameWork:others:2.1.9'

    //https://github.com/greenrobot/greenDAO
    implementation 'org.greenrobot:greendao:3.3.0' // add library

    // 悬浮窗框架：https://github.com/getActivity/EasyWindow
    implementation 'com.github.getActivity:EasyWindow:11.3'



    /**
     * https://stackoverflow.com/questions/75263047/duplicate-class-in-kotlin-android
     */
    constraints {
        /**
         * java.util.concurrent.ExecutionException: java.lang.RuntimeException:
         *     Duplicate class kotlin.internal.jdk7.JDK7PlatformImplementations found in modules
         *         jetified-kotlin-stdlib-1.8.0.jar (org.jetbrains.kotlin:kotlin-stdlib:1.8.0)
         *     and jetified-kotlin-stdlib-jdk7-1.5.0.jar (org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.0)
         */
        //https://github.com/JetBrains/kotlin/tree/master/libraries/stdlib/jdk7
        //依赖版本列表: https://central.sonatype.com/artifact/org.jetbrains.kotlin/kotlin-stdlib-jdk7
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        /**
         * java.util.concurrent.ExecutionException: java.lang.RuntimeException:
         *     Duplicate class kotlin.collections.jdk8.CollectionsJDK8Kt found in modules
         *         jetified-kotlin-stdlib-1.8.0.jar (org.jetbrains.kotlin:kotlin-stdlib:1.8.0)
         *     and jetified-kotlin-stdlib-jdk8-1.6.0.jar (org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0)
         */
        //依赖版本列表: https://central.sonatype.com/artifact/org.jetbrains.kotlin/kotlin-stdlib-jdk8
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

static def buildTime() {
    return new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("GMT+08:00"))
}